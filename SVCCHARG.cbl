IDENTIFICATION DIVISION.
PROGRAM-ID. SVCCHARG.
ENVIRONMENT DIVISION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
    SELECT TRANS-FILE ASSIGN TO TRANSIN
           FILE STATUS IS WS-TR-STAT.
    SELECT MASTER-FILE ASSIGN TO MASTER
            ORGANIZATION IS INDEXED
            ACCESS MODE IS RANDOM
            RECORD KEY IS ACCT-NO
            FILE STATUS IS WS-MA-STAT.
DATA DIVISION.
FILE SECTION.
FD  TRANS-FILE.
01  TRANS-REC.
    05 TR-ACCT-NO      PIC X(10).
    05 TR-COUNT        PIC 9(3).
FD  MASTER-FILE.
01  MASTER-REC.
    05 ACCT-NO         PIC X(10).
    05 ACCT-TYPE       PIC X(1).
    05 ACCT-BAL        PIC 9(7)V99.
    05 FILLER          PIC X(60).
WORKING-STORAGE SECTION.
01  WS-STATS.
       05 WS-TR-STAT      PIC XX.
       05 WS-MA-STAT      PIC XX.
       05 WS-PROC-CNT     PIC 9(4) VALUE 0.
       05 WS-DEDUCT-CNT   PIC 9(4) VALUE 0.
       05 WS-FAIL-CNT     PIC 9(4) VALUE 0.
       05 WS-TOTAL-AMT    PIC 9(7)V99 VALUE 0.
01  WS-CALCS.
       05 WS-CHARGE       PIC 9(5)V99.
PROCEDURE DIVISION.
000-MAIN.
    OPEN INPUT TRANS-FILE.
    OPEN I-O   MASTER-FILE.
           
    PERFORM 100-READ-TRANS.
           
    PERFORM UNTIL WS-TR-STAT = '10'
        ADD 1 TO WS-PROC-CNT
        MOVE TR-ACCT-NO TO ACCT-NO
        READ MASTER-FILE
        IF WS-MA-STAT = '00'
           PERFORM 200-PROCESS-LOGIC
        ELSE
           DISPLAY 'RECORD NOT FOUND: ' TR-ACCT-NO
           ADD 1 TO WS-FAIL-CNT
        END-IF
        PERFORM 100-READ-TRANS
    END-PERFORM.

    PERFORM 300-PRINT-SUMMARY.
    CLOSE TRANS-FILE MASTER-FILE.
    STOP RUN.

100-READ-TRANS.
    READ TRANS-FILE.

200-PROCESS-LOGIC.
    IF ACCT-TYPE = 'S'
       IF TR-COUNT > 5
          COMPUTE WS-CHARGE = (TR-COUNT - 5) * 15
       ELSE
          MOVE 0 TO WS-CHARGE
       END-IF
    ELSE
       MOVE 200 TO WS-CHARGE
    END-IF.

    IF ACCT-BAL >= WS-CHARGE
       SUBTRACT WS-CHARGE FROM ACCT-BAL
       REWRITE MASTER-REC
       ADD 1 TO WS-DEDUCT-CNT
       ADD WS-CHARGE TO WS-TOTAL-AMT
    ELSE
       DISPLAY 'INSUFFICIENT BALANCE: ' ACCT-NO
       ADD 1 TO WS-FAIL-CNT
    END-IF.

300-PRINT-SUMMARY.
    DISPLAY '------ SERVICE CHARGE SUMMARY ------'.
    DISPLAY 'Accounts Processed : ' WS-PROC-CNT.
    DISPLAY 'Charges Deducted   : ' WS-DEDUCT-CNT.
    DISPLAY 'Failed Deductions  : ' WS-FAIL-CNT.
    DISPLAY 'Total Charges      : ' WS-TOTAL-AMT.
    DISPLAY 'Batch Completed Successfully'.
